import groovy.lang.GroovyClassLoader

println "[BOOTSTRAP] Initializing..."

File workspace = new File(".").canonicalFile
println "[BOOTSTRAP] Workspace: ${workspace}"

File srcDir       = new File(workspace, "src")
File testsDir     = new File(workspace, "tests/src")
File varsDir      = new File(workspace, "vars")
File resourcesDir = new File(workspace, "resources")

GroovyClassLoader loader = new GroovyClassLoader(this.class.classLoader)

/* ---------------- SKIP GUARD (JENKINS-ONLY) ---------------- */
def isJenkinsOnly = { File f ->
    def txt = f.text
    return (
        txt.contains("jenkins.model.Jenkins") ||
        txt.contains("hudson.") ||
        txt.contains("com.cloudbees.groovy.cps") ||
        txt.contains("@NonCPS")
    )
}

/* Compile src/** + tests/** as CLASSES */
[srcDir, testsDir].each { dir ->
    if (dir.exists()) {
        loader.addClasspath(dir.absolutePath)
    }
}

/* Precompile src + tests (skip Jenkins-only) */
[srcDir, testsDir].each { dir ->
    if (dir.exists()) {
        dir.eachFileRecurse { f ->
            if (f.name.endsWith(".groovy")) {
                if (isJenkinsOnly(f)) {
                    println "[SKIP] Jenkins-only class: ${f}"
                } else {
                    loader.parseClass(f)
                }
            }
        }
    }
}

/* Load vars/** as SCRIPT steps */
Map vars = [:]

if (varsDir.exists()) {
    varsDir.eachFile { file ->
        if (file.name.endsWith(".groovy")) {
            if (isJenkinsOnly(file)) {
                println "[SKIP] Jenkins-only vars file: ${file}"
            } else {
                def clazz = loader.parseClass(file)
                def inst  = clazz.newInstance()
                def name  = file.name - ".groovy"

                vars[name] = inst
                // allows: deployInfra(...)
                this.binding.setProperty(name, inst.&call)
            }
        }
    }
}

/* Helpers */
def resource = { String path ->
    File f = new File(resourcesDir, path)
    if (!f.exists()) throw new RuntimeException("Missing resource: $path")
    f
}

def stage = { String name, Closure body ->
    println "\n=============================="
    println " STAGE : ${name}"
    println "=============================="
    body()
}

this.binding.setProperty("vars", vars)
this.binding.setProperty("resource", resource)
this.binding.setProperty("stage", stage)
this.binding.setProperty("env", [:])
this.binding.setProperty("params", [:])

println "[BOOTSTRAP] Ready"
println "--------------------------------------------------"
